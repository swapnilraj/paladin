/*
 * Copyright © 2024 Kaleido, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License"); you may not use this file except in compliance with
 * the License. You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software distributed under the License is distributed on
 * an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the License for the
 * specific language governing permissions and limitations under the License.
 *
 * SPDX-License-Identifier: Apache-2.0
 */

task besuBootstrapTool(type: Exec) {
    // Only build if the image doesn't exist
    onlyIf {
        def result = exec {
            executable "docker"
            args "image", "inspect", "paladin/besu_bootstrap"
            ignoreExitValue true
            standardOutput OutputStream.nullOutputStream()
            errorOutput OutputStream.nullOutputStream()
        }
        
        if (result.exitValue != 0) {
            println "Docker image paladin/besu_bootstrap not found, building..."
            return true
        } else {
            println "Docker image paladin/besu_bootstrap exists, skipping build"
            return false
        }
    }
    
    executable "docker"
    args "build"
    args "-t", "paladin/besu_bootstrap"
    args "-f", "besu_bootstrap/Dockerfile"
    args "."
}

task startTestInfra(type: DockerCompose, dependsOn: tasks.besuBootstrapTool) {
    composeFile 'docker-compose-test.yml'
    projectName 'paladin-testinfra'
    args 'up', '-d', '--wait', '--wait-timeout', 60
}


task stopTestInfra(type: DockerCompose) {
    composeFile 'docker-compose-test.yml'
    projectName 'paladin-testinfra'
    args 'down', '-v'
}

task removeBesuBootstrapTool(type: Exec, dependsOn: stopTestInfra) {
    executable "docker"
    args "rmi", "-f", "paladin/besu_bootstrap"
    errorOutput OutputStream.nullOutputStream()
    ignoreExitValue true
}

task clean(type: Delete, dependsOn: [tasks.removeBesuBootstrapTool, tasks.stopTestInfra]) {
}

// ── Nethermind alternative infrastructure ──────────────────────────────────
// Drop-in replacement for startTestInfra/stopTestInfra using Nethermind.
// Covers the free-gas chain only (equivalent to besu_free on ports 8545/8546).
// No bootstrap image is required; Nethermind's built-in dev config is used.
//
// Usage:
//   ./gradlew startNethermindInfra   # start postgres + nethermind
//   ./gradlew stopNethermindInfra    # stop and remove volumes
//
// After starting, point the component tests at Nethermind:
//   export BESU_PORT=8545   (or edit core/go/componenttest/.env)

task startNethermindInfra(type: DockerCompose) {
    composeFile 'docker-compose-nethermind.yml'
    projectName 'paladin-nethermind'
    args 'up', '-d', '--wait', '--wait-timeout', 60
}

task stopNethermindInfra(type: DockerCompose) {
    composeFile 'docker-compose-nethermind.yml'
    projectName 'paladin-nethermind'
    args 'down', '-v'
}
